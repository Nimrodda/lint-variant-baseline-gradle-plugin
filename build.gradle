plugins {
    id "java-gradle-plugin"
    id "org.jetbrains.kotlin.jvm" version "1.3.60"
    id "com.github.ben-manes.versions" version "0.27.0"
    id "org.jmailen.kotlinter" version "2.1.2"
    id "com.gradle.plugin-publish" version "0.10.1"
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.android.tools.build:gradle:3.5.3'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

gradlePlugin {
    plugins {
        androidLintVariantBaseline {
            displayName = "Android Lint Variant Baseline"
            description = "Variant specific Lint baseline file support for Android projects"
            id = 'com.nimroddayan.lint-variant-baseline'
            implementationClass = 'com.nimroddayan.androidlintvariantbaseline.AndroidLintVariantBaselinePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/Nimrodda/lint-variant-baseline-gradle-plugin'
    vcsUrl = 'https://github.com/Nimrodda/lint-variant-baseline-gradle-plugin'
    tags = ['android', 'lint', 'baseline', 'analysis']
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}

group 'com.nimroddayan.lint-variant-baseline'
version '0.1.1'

java {
    withJavadocJar()
    withSourcesJar()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    repositories {
        maven {
            url project.buildDir
        }
    }
}

// Publishing to bintray is done via task: bintrayUpload
bintray {
    user = bintray_user
    key = bintray_api_key
    publications = ['androidLintVariantBaselinePluginMarkerMaven', 'pluginMaven']
    publish = true
    pkg {
        repo = 'release'
        name = project.name
        userOrg = 'nimroddayan'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Nimrodda/lint-variant-baseline-gradle-plugin'
        version {
            name = project.version
            gpg {
                sign = true
            }
        }
    }
}

bintrayUpload.dependsOn publish
